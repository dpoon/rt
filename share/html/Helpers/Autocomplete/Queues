% $r->content_type('application/json');
<% JSON( \@suggestions ) |n %>
% $m->abort;
<%ARGS>
$term       => undef
$max        => 10
$op         => 'STARTSWITH'
$right      => undef
$valuefield => 'Name';
</%ARGS>
<%INIT>
$m->abort unless defined $term
             and length $term;

# Sanity check the operator
$op = 'STARTSWITH' unless $op =~ /^(?:LIKE|(?:START|END)SWITH|=|!=)$/i;

my $queues = RT::Queues->new( $session{CurrentUser} );
$queues->RowsPerPage( $max );
$queues->Limit(
    FIELD           => 'Name',
    OPERATOR        => $op,
    VALUE           => $term,
    ENTRYAGGREGATOR => 'OR',
    CASESENSITIVE   => 0,
);

my @suggestions;
while (my $q = $queues->Next) {
    next if $right and not $q->CurrentUserHasRight($right);
    my $value = lc($valuefield) eq 'id' ? $q->Id : $q->Name;
    push @suggestions, { label => $q->Name, value => $value };
}
</%INIT>
